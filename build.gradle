apply plugin: 'java'

buildscript {
    ext {
        // 读取所有库的版本信息，
        def versionFile = rootProject.file('version.properties')
        if (versionFile.exists()) {
            def input = versionFile.newInputStream()
            def p = new Properties()
            p.load(input)
            p.forEach { k, v ->
                ext.set(k, v)
            }
        }
        // 读取上传仓库相关配置，示例在publish.properties.example,
        def publishFile = rootProject.file('publish.properties')
        hasPublish = publishFile.exists()
        if (hasPublish) {
            def input = publishFile.newInputStream()
            pub = new Properties()
            pub.load(input)
        }
    }
    repositories {
        jcenter()
    }
    dependencies {
        // 使用bintray-release上传bintray也就是jcenter，
        classpath 'com.novoda:bintray-release:' + bintray_version
    }
}
sourceSets {
    main {
        java.srcDirs = ['src']
    }
}
// 上传仓库的组名优先从publish.properties中拿，
group = 'cc.aoeiuv020'
version = version_name

// 固定编译器识别编码utf-8,
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'com.novoda.bintray-release'

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

// 配置上传bintray也就是jcenter,
// 如果没有publish.properties或者没有key属性，就不上传，也就是dryRun=true,
publish {
    // 都是publish.properties优先的，这里赋值避免没有publish.properties时gradle同步失败，
    userOrg = 'aoeiuv020'
    bintrayUser = userOrg
    artifactId = 'adb-lib'
    publishVersion = version
    desc = "Java library $name."
    website = 'https://github.com/AoEiuV020/AdbLib'
    groupId = group
    if (hasPublish) {
        dryRun = false
        // publish.properties里和这个publish闭包参数同名，参考bintray-release官方wiki，
        // https://github.com/HailouWang/bintray-release/wiki/%E9%85%8D%E7%BD%AEpublish%E9%97%AD%E5%8C%85
        properties.forEach { key, value ->
            if (pub.getProperty(key)?.trim()) {
                setProperty(key, pub.getProperty(key))
            }
        }
        // 字符串转成数组，逗号分隔，
        // 因为配置文件无法存数组，
        if (pub.licences?.trim()) {
            licences = pub.licences.split(',')
        }
    }
    // 如果没有配置bintrayKey就不上传，
    if (!bintrayKey?.trim()) {
        dryRun = true
    }
}
publishToMavenLocal.dependsOn('assemble')
